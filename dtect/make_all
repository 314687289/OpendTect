#!/bin/csh -f

set mods=.mods
set shallow=no
set dodeps=yes
set doupd=yes
set dodoc=no

source $WORK/Pmake/base/PMinit.csh

parse_args:
if ( "$1" == "--shallow" ) then
    set shallow=yes
else if ( "$1" == "--nodep" ) then
    set dodeps=no
else if ( "$1" == "--noupd" ) then
    set doupd=no
else if ( "$1" == "--od" ) then
    set mods=.mods.od
else if ( "$1" == "--doc" ) then
    set dodoc=yes
else if ( "$1" == "--help" ) then
    echo "Usage: make_all [option]"
    echo " --shallow, Do not remove libs"
    echo " --nodep, Do not create deps"
    echo " --mkdep, Do create deps"
    echo " --noupd, Do not run 'dvs update'"
    echo " --od, Only make OpendTect"
    echo " --doc, Create ClassDoc"
    exit 0
else
    goto do_it
endif

shift
goto parse_args

do_it:

set hostnm=`hostname`
if ( $USER == dgb && ($hostnm == dgb3 || $hostnm == dgbindia10) ) then
    cdw
    cd ..
    set oddir=$WORK:t
    set dgbdir=`echo $oddir | sed 's/od/dgb/'`
    if ( $doupd == yes ) then
	echo "dvs update $oddir $dgbdir"
	dvs update $oddir $dgbdir
    endif
    cdw
    goto programmer_doc
endif

if ( $hostnm == dgb26 ) then
    cdw; cd src; ./cpdeps
    cdw; cd plugins; ./cpdeps
    cdw; cd spec; ./cpdeps
    set mods=.mods.noui
endif

if ( ! -d $WORK/dtect/build ) then
    mkdir $WORK/dtect/build
endif

set makedonefile="$WORK/dtect/build/make_done"
if ( -e $makedonefile ) rm $makedonefile

if ( $shallow == no ) then
    cdl
    echo "Removing all libs from " `pwd`
    if ( $HDIR == win ) then
	\rm *.lib
	\rm *.dll
	\rm *.dll.a
    else if ( $HDIR == mac ) then
	\rm *.dylib
	\rm *.a
    else
	\rm *.a
    endif
    ln -s ../*.a .
endif

cdw

if ( $doupd == yes ) then
    set oddir=$WORK:t
    set dgbdir=`echo $oddir | sed 's/od/dgb/'`
    cd ..
    echo "dvs update $oddir $dgbdir"
    dvs update $oddir $dgbdir
    cdw
endif

if ( $dodeps == yes ) then
    make dep MODS=$mods
    cd plugins ; make dep
    cd ../spec ; make dep
    cd ..
endif

make lib MODS=$mods
make MODS=$mods
cd plugins
make
cd ../spec
make

programmer_doc:

if ( $dodoc == yes ) then
    cdw
    make doc MODS=.mods.od
    cd doc/Programmer/Generated
    ./mk_paths_relative
    GenModDeps --html $WORK/Pmake/ModDeps.od index.html
endif

if ( $hostnm == dgb3 ) exit 0
touch $makedonefile
