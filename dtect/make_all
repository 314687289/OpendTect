#!/bin/csh -f

set mods=.mods.dtect
if ( $HDIR == lux || $HDIR == sun5 ) then
    if ( $PLFSUBDIR != lux64 ) set mods=.mods
endif
set shallow=no
set dodeps=yes
set docpdeps=yes
set dodoc=no
set doupd=yes

source $WORK/Pmake/base/PMinit.csh

parse_args:
if ( "$1" == "--shallow" ) then
    set shallow=yes
else if ( "$1" == "--dtect" ) then
    set mods=.mods.dtect
else if ( "$1" == "--gdi" ) then
    set mods=.mods.gdi
else if ( "$1" == "--nodep" ) then
    set dodeps=no
else if ( "$1" == "--mkdep" ) then
    set dodeps=yes
    set docpdeps=no
else if ( "$1" == "--noupd" ) then
    set doupd=no
else if ( "$1" == "--doc" ) then
    set dodoc=yes
else if ( "$1" == "--help" ) then
    echo "Usage: make_all [option]"
    echo " --shallow, Do not remove libs"
    echo " --nodep, Do not create deps"
    echo " --noupd, Do not run 'dvs update'"
    echo " --dtect, Only make dTect"
    echo " --gdi, Only make GDI"
    echo " --doc, Create ClassDoc"
    exit 0
else
    goto do_it
endif

shift
goto parse_args

do_it:

if ( ! -d $WORK/dtect/build ) then
    mkdir build
endif

set depsdonefile="$WORK/dtect/build/deps_done"
set makedonefile="$WORK/dtect/build/make_done"
if ( -e $depsdonefile ) rm $depsdonefile
if ( -e $makedonefile ) rm $makedonefile

if ( $shallow == no ) then
    cdl
    echo "Removing all libs from " `pwd`
    if ( $HDIR == win ) then
	\rm *.lib
	\rm *.dll
	\rm *.dll.a
    else
	\rm *.a
    endif
    ln -s ../*.a .
endif

cdw

if ( $doupd == yes ) then
    dvs update
endif

if ( $dodeps == yes ) then

    if ( `hostname` == $MAIN_DGB_HOST ) set docpdeps=no
    if ( $HDIR == win ) set docpdeps=no

    if( $docpdeps == no ) then
	make dep MODS=$mods
	cd plugins ; make dep
	cd ../spec ; make dep
	cd ..
	touch $depsdonefile
    else
	cd src ; ./cpdeps
	cd ../plugins ; ./cpdeps
	cd ../spec ; ./cpdeps
	cd ..
    endif
endif
make lib MODS=$mods
make MODS=$mods
cd plugins
make
cd ../spec
make

if ( $dodoc == yes ) then
    cdw
    make doc MODS=.mods.od
    cd data/dTectDoc/Programmer/Generated
    ./mk_paths_relative
    GenModDeps --html /d3/dgb/Pmake/ModDeps.od index.html
endif

touch $makedonefile
