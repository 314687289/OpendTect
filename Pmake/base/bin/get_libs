#!/bin/csh
#__Pmake________________________________________________________________________
#
# $Id: get_libs,v 1.1 2007-06-28 12:40:15 cvsbert Exp $
# This script returns a libname for a given link directive
#_______________________________________________________________________________

set testexist=0
if ( "$1" == "--testexist" ) then
	set testexist=1
	shift
endif
set fullnms=0
if ( "$1" == "--expand" ) then
	set fullnms=1
	shift
endif

set libsubd=$1
shift

set dynext=so
set libprfx=lib
if( $HDIR == "win" ) then
    set dynext=dll
    set libprfx=""
else if ( $HDIR == "mac" ) then
    set dynext=dylib
endif

#set verbose=yes
#set echo=on

foreach inp ( $* )

    set libnm=`echo $inp|awk '{ print substr($1,3,length($1)); }'`
    set libnm=${libprfx}${libnm}.${dynext}
    set fulllibnm="$WORK/lib/$libsubd/$libnm"

    if ( $testexist == 0 || -e "$fulllibnm" ) then
	if ( $fullnms == 1 ) then
	    echo -n " $fulllibnm"
	else
	    echo -n " $inp"
	endif
    endif

end

exit 0
