#!/bin/csh
#__Pmake________________________________________________________________________
#
# This script creates a shared object lib from a number of input libraries
# SCCS %Z%%M%   Version: %I%  Date: %G% - %U%
#_______________________________________________________________________________


if ( $#argv < 3 ) then
	echo "Usage: $0 Input_lib Output_sharedlib [win: deffile] Linker_and_flags"
	exit 1
endif



set inplibs=""
foreach lib ( $* )

    if ( "$lib" == "--output" ) goto inplibs_done

    if ( ! -r "$lib" ) then
	    echo "$0 : Input lib $lib cannot be accessed"
	    exit 1
    endif

    set inplibs="$inplibs $lib"
    shift

end
inplibs_done:
shift

set outlib=$1
shift


if ( $HDIR != "win") then
    set tmpdir=`$PMAKE/bin/get_temp_file_name`

    if ( ! -d $tmpdir ) mkdir $tmpdir
    if ( ! -d $tmpdir ) then
	    echo "$0 : Cannot create $tmpdir"
	    exit 1
    endif

    cd $tmpdir

    foreach lib ( $inplibs )
	    ar x $lib `ar t $lib`
    end

    #echo "Executing:" $* *.o -o $outlib
    $* *.o -o $outlib
    cd /tmp

    rm -rf $tmpdir

else

    echo "Executing:" $* -o $outlib
    $* -o $outlib

endif



exit 0
