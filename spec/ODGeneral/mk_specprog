#!/bin/csh
#
# Script to compile low-level, basic programs.
# These must be linked statically, because the shared libs may not be
# available (yet)


if ( $#argv < 1 ) then
	echo "Usage: $0 program_name"
	exit 1
endif

set prog=$1
set debugdir=G
if ( $?DEBUG ) then
    if ( $DEBUG == no ) then
      set debugdir=O
      unsetenv DEBUG
    endif
else
    set debugdir=O
endif

set libdir=$WORK/lib/$PLFSUBDIR/${debugdir}
set incdir=$WORK/include
set gmdexec=$WORK/bin/$PLFSUBDIR/${debugdir}/$prog
set extraflgs=""
set libpref="lib"
set libext=".a"
set mylibs="-lpthread -lm -ldl"

if ( $HDIR == sgi ) then
	set extraflgs="-L/usr/lib32"
else if ( $HDIR == sun5 ) then
	set mylibs="-lnsl"
else if ( $HDIR == win ) then
	set extraflgs="-mno-cygwin"
	set libpref=""
	set libext=".lib"
	set mylibs="-lmsvcrt -lmsvcrt -lshlwapi -lwsock32 -lole32 -luuid"
	set gmdexec=$WORK/bin/$PLFSUBDIR/${debugdir}/$prog.exe
else if ( $HDIR == mac ) then
	set extraflgs="-framework CoreFoundation -framework ApplicationServices -headerpad_max_install_names -arch x86_64"
endif

set comp=g++
if ( $?OWNGCCDIR ) then
    set comp=$OWNGCCDIR/bin/g++
endif

if ( $?DEBUG ) then
    ${comp} -ggdb3 -D$HDIR -fsigned-char -Wconversion -Wparentheses -Wreturn-type -Werror -Wno-non-template-friend  -I$incdir -I$incdir/Basic -I$incdir/Prog -D__prog__ -o $gmdexec $prog.cc $libdir/${libpref}Basic${libext} $extraflgs -lstdc++ -lQtCore -L$OD_QTDIR/lib ${mylibs}

else

    ${comp} -O3 -D$HDIR -fsigned-char -Wconversion -Wparentheses -Wreturn-type -Werror -Wno-non-template-friend  -I$incdir -I$incdir/Basic -I$incdir/Prog -D__prog__ -o $gmdexec $prog.cc $libdir/${libpref}Basic${libext} $extraflgs -lstdc++ -lQtCore -L$OD_QTDIR/lib ${mylibs}

    strip $gmdexec

endif
