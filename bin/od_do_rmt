#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: od_do_rmt,v 1.8 2004-02-04 15:11:24 arend Exp $
#
# remote execution script
#_______________________________________________________________________________

set comm=""

next_arg:

if ( $1 == "+-+" ) then

	shift
	eval $comm
	set comm=""
	goto next_arg

else if ( $1 == "+-+-+" ) then

	shift
	eval $comm
	goto do_rest

else

	set comm="$comm $1"
	shift
	goto next_arg

endif

do_rest:

if ( -e $DTECT_APPL/bin/init_dtect ) then
    source $DTECT_APPL/bin/init_dtect
endif

if ( $?DTECT_DEBUG ) then

    set logfile=/tmp/oddebug$$.log
    echo "DTECT_DATA = $DTECT_DATA" > $logfile
    echo "DTECT_USER = $DTECT_USER" >> $logfile
    echo "DTECT_APPL = $DTECT_APPL" >> $logfile
    if ( $?LD_LIBRARY_PATH ) then
	echo "LD_LIBRARY_PATH = $LD_LIBRARY_PATH" >> $logfile
    else
	echo "No LD_LIBRARY_PATH set!" >> $logfile
    endif
    if ( $?LM_LICENSE_FILE ) then
	echo "LM_LICENSE_FILE = $LM_LICENSE_FILE" >> $logfile
    else
	echo "No LM_LICENSE_FILE set!" >> $logfile
    endif
    echo "DISPLAY = $DISPLAY" >> $logfile
    echo "$DTECT_APPL/bin/od_exec $*" >> $logfile

    $DTECT_APPL/bin/od_exec $* >> $logfile

else

    $DTECT_APPL/bin/od_exec $*

endif

exit $status
