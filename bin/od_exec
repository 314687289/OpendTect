#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) 1999 de Groot-Bril Earth Sciences B.V.
# $Id: od_exec,v 1.8 2002-11-26 13:04:51 dgb Exp $
#
# module startup script
#_______________________________________________________________________________

if ( ! $?dGB_APPL ) then
	echo "Error : dGB program not initialized."
	exit 1
else if ( ! $?HDIR ) then
	source $dGB_APPL/bin/sethdir
endif

set binsubdir=$HDIR
if ( $binsubdir == sun5 ) then
    if ( -d $dGB_APPL/bin/sol26 || -d $dGB_APPL/bin/sol28 ) then
	set v=`uname -r|sed 's/5\.//'`
	if ( "$v" == "6" || "$v" == "7" ) then
	    set binsubdir=sol26
	else if ( -d $dGB_APPL/bin/sol28 \
		&& ( "$v" == "8" || "$v" == "9" ) ) then
	    set binsubdir=sol28
	endif
    endif
endif
if ( ! -d $dGB_APPL/bin/$binsubdir ) then
    set binsubdir=$HDIR
endif

set bindir="$dGB_APPL/bin/$binsubdir"
if ( ! -d $bindir ) then
	echo "Fatal error: Cannot start program because directory:"
	echo "$bindir"
	echo "Does not exist."
	exit 1
endif

setenv XAPPLRESDIR	$dGB_APPL/data
setenv PATH		"${bindir}:${PATH}"
if ( $?LD_LIBRARY_PATH ) then
	setenv LD_LIBRARY_PATH	"${bindir}/so:${LD_LIBRARY_PATH}"
else
	setenv LD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
endif

set niceness=0
if ( $1 == --nice ) then
	set niceness=$2
	set nicecmd=/usr/bin/nice
	if ( -x /bin/nice ) set nicecmd=/bin/nice
	if ( ! -x $nicecmd ) then
		set niceness=0
	else
		set nicecmd="$nicecmd -$2"
	endif
	shift ; shift
endif

if ( $niceness == 0 ) then
	$bindir/$*
else
	$nicecmd $bindir/$*
endif
exit $status
