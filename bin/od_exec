#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) 1999 de Groot-Bril Earth Sciences B.V.
# $Id: od_exec,v 1.4 2002-05-30 13:49:59 bert Exp $
#
# module startup script
#_______________________________________________________________________________

if ( ! $?dGB_APPL ) then
	echo "Error : dGB program not initialized."
	exit 1
else if ( ! $?HDIR ) then
	source $dGB_APPL/bin/sethdir
endif

set binsubdir=$HDIR
if ( $binsubdir == sun5 ) then
    if ( -d $dGB_APPL/bin/sol26 ) then
	set v=`uname -r|sed 's/5\.//'`
	if ( "$v" == "6" || "$v" == "7" ) then
	    set binsubdir=sol26
	else if ( -d $dGB_APPL/bin/sol28 \
		&& ( "$v" == "8" || "$v" == "9" ) ) then
	    set binsubdir=sol28
	endif
    endif
endif

set bindir="$dGB_APPL/bin/$HDIR"

setenv XAPPLRESDIR	$dGB_APPL/data
setenv PATH		"${bindir}:${PATH}"
if ( $?LD_LIBRARY_PATH ) then
	setenv LD_LIBRARY_PATH	"${bindir}/so:${LD_LIBRARY_PATH}"
else
	setenv LD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
endif

set niceness=0
if ( $1 == --nice ) then
	set niceness=$2
	shift ; shift
endif

if ( $niceness == 0 ) then
	$bindir/$*
else
	/usr/bin/nice -$niceness $bindir/$*
endif
exit $status
