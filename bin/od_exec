#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) 1999 de Groot-Bril Earth Sciences B.V.
# $Id: od_exec,v 1.18 2003-05-22 11:10:27 bert Exp $
#
# module startup script
#_______________________________________________________________________________

if ( ! $?dGB_APPL ) then
	echo "Error : dGB program not initialized."
	exit 1
else if ( ! $?HDIR ) then
	source $dGB_APPL/bin/sethdir
endif

set binsubdir=$HDIR
if ( $binsubdir == sun5 ) then
    if ( -d $dGB_APPL/bin/sol26 || -d $dGB_APPL/bin/sol28 ) then
	set v=`uname -r|sed 's/5\.//'`
	if ( "$v" == "6" || "$v" == "7" ) then
	    set binsubdir=sol26
	else if ( -d $dGB_APPL/bin/sol28 \
		&& ( "$v" == "8" || "$v" == "9" ) ) then
	    set binsubdir=sol28
	endif
    endif
endif
if ( ! -d $dGB_APPL/bin/$binsubdir ) then
    set binsubdir=$HDIR
endif

set bindir="$dGB_APPL/bin/$binsubdir"
set nicecmd=""
set extrascript=""
set extraargs=""
set xtermcmd=""
set issys=no

next_arg:

if ( "$1" == "--sys" ) then

	set issys=yes
	shift
	goto next_arg

else if ( "$1" == "--nice" ) then

	set niceness=$2
	set nicecmd=/usr/bin/nice
	if ( -x /bin/nice ) set nicecmd=/bin/nice
	if ( ! -x $nicecmd ) then
		set nicecmd=""
	else
		set nicecmd="$nicecmd -$2"
	endif

	shift ; shift
	goto next_arg

else if ( "$1" == "--prescript" ) then

	set extrascript="$2"
	if ( ! -e $extrascript ) then
	    if ( -e $dGB_APPL/bin/$extrascript ) then
		set extrascript="$dGB_APPL/bin/$extrascript"
	    else if ( -e $bindir/$extrascript ) then
		set extrascript="$bindir/$extrascript"
	    endif
	endif

	shift ; shift
	goto next_arg

else if ( "$1" == "--inxterm" || "$1" == "--inxterm+askclose" ) then

	set xtermcmdarg=""
	if ( "$1" == "--inxterm+askclose" ) then
	    set xtermcmdarg = "--askclose"
	endif

	set xtermcmd="xterm -bg lightyellow -fg black -geometry 80x25 -e $dGB_APPL/bin/dgb_xterm_exec $xtermcmdarg"

	shift
	goto next_arg

endif


if ( ! -d $bindir ) then
	echo "Fatal error: Cannot start program because directory:"
	echo "$bindir"
	echo "Does not exist."
	exit 1
endif

setenv XAPPLRESDIR	$dGB_APPL/data
setenv PATH		"${bindir}:${PATH}"
if ( $?LD_LIBRARY_PATH ) then
	setenv LD_LIBRARY_PATH	"${bindir}/so:${LD_LIBRARY_PATH}"
else
	setenv LD_LIBRARY_PATH	"${bindir}/so:/lib:/usr/lib"
endif
unsetenv QTDIR


if ( $extrascript != "" ) then
	source $extrascript
endif


if ( $issys == yes ) then
	if ( $?dGB_DEBUG ) echo "$0 : $xtermcmd $nicecmd $* $extraargs"
	$xtermcmd $nicecmd $* $extraargs
else
	if ( $?dGB_DEBUG ) echo "$0 : $xtermcmd $nicecmd $bindir/$* $extraargs"
	$xtermcmd $nicecmd $bindir/$* $extraargs
endif

exit $status
