#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: mksethdir,v 1.12 2004-12-10 10:48:53 dgb Exp $
#
# HDIR variable setup
# usage from a csh-script:
#    eval `/__path__/mksethdir <root binary dir>`
#    if no root binary dir given, $DTECT_APPL is used
#_______________________________________________________________________________

set sethdirstr=""
if ( ! $?HDIR ) then
    setenv HDIR `uname`

    if ( "$HDIR" == "SunOS" ) then
	setenv HDIR sun5
    else if ( "$HDIR" == "Linux" ) then
	setenv HDIR lux
    else if ( "$HDIR" == "Darwin" ) then
	setenv HDIR mac
    else if ( "$HDIR" == "IRIX" || "$HDIR" == "IRIX64" ) then
	setenv HDIR sgi
    else if ( $?COMSPEC ) then
	setenv HDIR win
    endif

    set sethdirstr="setenv HDIR $HDIR; "
endif

set ROOT_DIR="$1"
if ( $ROOT_DIR == "" ) then
    if ( $?DTECT_APPL ) then
        set ROOT_DIR="$DTECT_APPL"
    endif
endif

set binsubdir=$HDIR
if ( $binsubdir == sun5 ) then
    set binsubdir=sol32
    if ( -d $ROOT_DIR/bin/sol64 ) then
	set binsubdir=lux64
    endif
    if ( -d $ROOT_DIR/bin/sol26 ) then
	set v=`uname -r|sed 's/5\.//'`
	if ( `uname -r | sed 's/5\.//'` < 8 ) set binsubdir=sol26
    endif
else if ( $binsubdir == lux ) then
    set binsubdir=lux32
    if ( -d $ROOT_DIR/bin/lux64 ) then
	if ( `uname -a | grep _64 | wc -l` == 1 ) set binsubdir=lux64
    endif
endif

if ( ! -d "$ROOT_DIR/bin/$binsubdir" ) then
    set binsubdir=$HDIR
endif

echo "$sethdirstr setenv binsubdir $binsubdir"
