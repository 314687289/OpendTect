#!/bin/csh
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: od_cr_dev_env,v 1.19 2005-03-01 13:59:58 cvsarend Exp $
#
# Script copies sources and libs into a development directory
#
#_______________________________________________________________________________

if (  $#argv < 2 ) then
    echo "Usage : $0 OpendtTect_directory existing_target_directory [--installcygwin]"
    exit 1
endif

set instcyg=0
if ( "$HDIR" == "win" ) then
    set inpdir=`cygpath -u "$1"` 
    set outdir=`cygpath -u "$2"`
    set soext=dll
    if ( "$3" == "--installcygwin" ) then
	set instcyg=1
    endif
else
    set inpdir="$1"
    set outdir="$2"
    set soext=so
endif

set cpcmd="cp -rp"

if ( "$HDIR" == "mac" ) then
    set soext=dylib
endif

if ( "$HDIR" == "lux" ) then
    set cpcmd="cp -a"
endif

if ( ! -d "$inpdir"  ||  ! -e "$inpdir/.rel.od.doc" ) then
    echo "$0 : $inpdir does not have the doc package installed"
    exit 1
endif

if ( ! -w "$outdir"  ) then
    echo "$0 : target directory $outdir not writable"
    exit 1
endif


# ----------------------------------------------------------------------------
# Setup work directory
# ----------------------------------------------------------------------------

cd "$outdir"

foreach item ( plugins src include Pmake )
    $cpcmd "$inpdir/$item" .
end

if ( -d plugins ) then
    cd plugins
    if ( -e Makefile.lnk ) then
	\rm Makefile.lnk
    endif
    if ( ! -e Makefile ) then
        ln -s Makefile.od Makefile
    endif
endif

cd "$outdir"

mkdir lib
cd lib
mkdir $HDIR
cd $HDIR
ln -s . G
ln -s . O

cd "$outdir"
mkdir bin
cd bin
mkdir $HDIR
cd $HDIR
ln -s . G
ln -s . O
mkdir so
cd so


$cpcmd "$inpdir/bin/$HDIR/so"/*.$soext .
if ( "$HDIR" == "win" ) then
    $cpcmd "$inpdir/bin/$HDIR/so"/*.a .
endif


cd "$outdir"

foreach fil ( init.bash init.csh )
    sed -e "s%__WORK__%$outdir%" \
        -e "s%__hdir__%$HDIR%" \
        -e "s%__DTECTAPPL__DIR__%$DTECT_APPL%" \
        "$inpdir/.$fil" > $fil
end

cp "$inpdir/.rel.od.doc" .

if ( $instcyg == 1 ) then
    cd "${inpdir}\\bin\\win"
    cmd /c start instcyg.bat
endif
