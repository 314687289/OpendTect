#!/bin/csh
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: od_cr_dev_env,v 1.24 2006-07-03 10:33:58 cvsbert Exp $
#
# Script copies sources and libs into a development directory
#
#_______________________________________________________________________________

if (  $#argv < 2 ) then
    echo "Usage : $0 OpendTect_directory existing_target_directory [--installcygwin]"
    exit 1
endif

set instcyg=0
if ( "$HDIR" == "win" ) then
    set inpdir=`cygpath -u "$1"` 
    set outdir=`cygpath -u "$2"`
    set soext=dll
    if ( "$3" == "--installcygwin" ) then
	set instcyg=1
    endif
else
    set inpdir="$1"
    set outdir="$2"
    set soext=so
endif

set cpcmd="cp -rp"

if ( "$HDIR" == "mac" ) then
    set soext=dylib
endif

if ( "$HDIR" == "lux" ) then
    set cpcmd="cp -a"
endif

if ( ! -d "$inpdir" ) then
    echo "$0 : $inpdir does not exist"
    exit 1
endif
if ( ! -e "$inpdir/.rel.od.doc" ) then
    echo "$0 : warning : $inpdir does not have the doc package installed"
endif

if ( ! -w "$outdir"  ) then
    echo "$0 : target directory $outdir not writable"
    exit 1
endif


# ----------------------------------------------------------------------------
# Setup work directory
# ----------------------------------------------------------------------------

cd "$outdir"

foreach item ( plugins src include Pmake )
    $cpcmd "$inpdir/$item" .
end

ln -s Pmake/RootMakefile Makefile
if ( -d plugins ) then
    cd plugins
    if ( -e Makefile.lnk ) then
	\rm Makefile.lnk
    endif
    if ( ! -e Makefile ) then
        ln -s Makefile.od Makefile
    endif
endif

cd "$outdir"

if ( ! $?binsubdir ) then
    if ( $?PLFSUBDIR ) then
	set binsubdir="$PLFSUBDIR"
    else
	set binsubdir="$HDIR"
    endif
endif

mkdir bin
cd bin
mkdir "$binsubdir"
if ( "$binsubdir" != "$HDIR" ) ln -s "$binsubdir" "$HDIR"
cd "$binsubdir"
ln -s . G
ln -s . O
mkdir so
cd so

${cpcmd} "${inpdir}/bin/${binsubdir}/so"/*.${soext}* .
if ( "$binsubdir" == "win" ) then
    $cpcmd "$inpdir/bin/$binsubdir/so"/*.a .
endif


cd "$outdir"
mkdir lib
cd lib
mkdir "$binsubdir"
if ( "$binsubdir" != "$HDIR" ) ln -s "$binsubdir" "$HDIR"
cd "$binsubdir"
ln -s . G
ln -s . O
ln -s ../../bin/"$binsubdir"/so/{libqt-mt.so.3,libfftw3f.so.3,libCoin.so.*,libSoQt.so.*} .
ln -s libqt-mt.so.3 libqt-mt.so
ln -s libfftw3f.so.3 libfftw3f.so
ln -s libCoin.so.* libCoin.so
ln -s libSoQt.so.* libSoQt.so



cd "$outdir"

foreach fil ( init.bash init.csh )
    sed -e "s%__WORK__%$outdir%" \
        -e "s%__hdir__%$binsubdir%" \
        -e "s%__DTECTAPPL__DIR__%$DTECT_APPL%" \
        "$inpdir/.$fil" > $fil
end

if ( -e "$inpdir/.rel.od.doc" ) cp "$inpdir/.rel.od.doc" .

if ( $instcyg == 1 ) then
    cd "${inpdir}\\bin\\win"
    cmd /c start instcyg.bat
endif
