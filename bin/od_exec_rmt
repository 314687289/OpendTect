#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: od_exec_rmt,v 1.13 2004-07-28 12:47:24 arend Exp $
#
# module remote startup script
#_______________________________________________________________________________

if ( $?DTECT_DEBUG ) then
    echo "$*"
endif

if ( ! $?DTECT_APPL ) then
	echo "Error : environment not initialized."
	exit 1
endif

if ( $#argv < 1 ) then
    echo "Usage: $0 remote_host [--rexec remote_shell_command]"
    echo "       [--with-dtect-appl <DTECT_APPL on remote host>"
    echo "       [--with-dtect-data <DTECT_DATA on remote host>"
    echo "       [--transfervars <list of environment vars to transfer> -+-+-"
endif

set remotehost=$1
shift

set rshcomm=rsh
set transfvars=""
set remoteuser=""
set dtectappl="$DTECT_APPL"
set dtectdata="$DTECT_DATA"

next_arg:

if ( "$1" == "--rexec" ) then
	set rshcomm=$2
	shift ; shift
	goto next_arg
else if ( $1 == "--remoteuser" ) then
        set remoteuser=" -l $2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-dtect-appl" ) then
        set dtectappl="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-dtect-data" ) then
        set dtectdata="$2"
        shift; shift
        goto next_arg
else if ( "$1" == "--transfervars" ) then
    next_var:
	shift
	if ( "$1" == "-+-+-" ) then
	    shift
	    goto next_arg
	endif

	set transfvars="$transfvars $1"
	goto next_var
endif

set transf="setenv DTECT_DATA $dtectdata +-+ setenv DTECT_APPL $dtectappl"

if ( $?DTECT_USER ) then
	set transf="${transf} +-+ setenv DTECT_USER $DTECT_USER"
endif

if ( $?LM_LICENSE_FILE ) then
	set transf="${transf} +-+ setenv LM_LICENSE_FILE $LM_LICENSE_FILE"
endif

if ( $?DISPLAY ) then
	set dnm=`echo $DISPLAY|sed 's/://'| awk '{print $1}'`
	if ( $dnm == local || $dnm == unix || $dnm == 0 || $dnm == "0.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		set nr=`echo $DISPLAY|sed 's/://'| awk '{print $2}'`
		if ( $nr == "" ) set nr=0
		setenv DISPLAY ${hona}":"${nr}.0
	else if ( $dnm == "1" || $dnm == "1.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		setenv DISPLAY ${hona}":1.0"
	endif
	set transf="${transf} +-+ setenv DISPLAY $DISPLAY"
endif

foreach var ( $transfvars )
    set dollarvar=`echo "$ ${var}" | sed "s/ //"`
    set varvalue=`eval "echo $dollarvar"`
    if ( $varvalue != "" ) then
	set transf="${transf} +-+ setenv $var $varvalue"
    endif
end

set transf="$transf +-+-+ $*"

if ( $?DTECT_DEBUG ) then
    echo "od_exec_rmt: " ${rshcomm} ${remoteuser} ${remotehost} ${dtectappl}/bin/od_do_rmt $transf
endif

${rshcomm} ${remoteuser} ${remotehost} "${dtectappl}/bin/od_do_rmt" $transf
exit 0
