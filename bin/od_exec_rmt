#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) dGB Beheer B.V.
# $Id: od_exec_rmt,v 1.14 2004-09-27 08:17:00 dgb Exp $
#
# module remote startup script
#_______________________________________________________________________________

if ( $?DTECT_DEBUG ) set verbose=yes

if ( $?DTECT_DEBUG ) then
    echo "od_exec_rmt $*"
endif

if ( ! $?DTECT_APPL ) then
	echo "Error : environment not initialized."
	exit 1
endif

if ( $#argv < 1 ) then
    echo "Usage: $0 remote_host [--rexec remote_shell_command]"
    echo "       [--with-dtect-appl <DTECT_APPL on remote host>"
    echo "       [--with-dtect-data <DTECT_DATA on remote host>"
    echo "       [--with-local-proc-dir <processing directory on local host>"
    echo "       [--with-remote-proc-dir <processing directory on remote host>"
    echo "       [--transfervars <list of environment vars to transfer> -+-+-"
endif

set remotehost=$1
shift

set rshcomm=rsh
set transfvars=""
set remoteuser=""
set remotepass=""
if ( $?DTECT_REMOTE_PASS ) then
    set remotepass="$DTECT_REMOTE_PASS"
endif
set dtectappl="$DTECT_APPL"
if ( $?DTECT_DATA ) then
    set dtectdata="$DTECT_DATA"
endif

set datahost=""
set datadrive=""
set datashare=""

set datahost="dgb27"
set datadrive="w"
set datashare="dw2k"

next_arg:

if ( "$1" == "--rexec" ) then
	set rshcomm=$2
	shift ; shift
	goto next_arg
else if ( $1 == "--remoteuser" ) then
        set remoteuser=" -l $2"
        shift; shift
        goto next_arg
else if ( $1 == "--remotepass" ) then
        set remotepass="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-dtect-appl" ) then
        set dtectappl="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-dtect-data" ) then
        set dtectdata="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-local-proc-dir" ) then
        set locprocdir="$2"
        shift; shift
        goto next_arg
else if ( $1 == "--with-remote-proc-dir" ) then
        set remprocdir="$2"
        shift; shift
        goto next_arg
else if ( "$1" == "--transfervars" ) then
    next_var:
	shift
	if ( "$1" == "-+-+-" ) then
	    shift
	    goto next_arg
	endif

	set transfvars="$transfvars $1"
	goto next_var
endif

set transf="setenv DTECT_DATA -=-$dtectdata-=- +-+ setenv DTECT_APPL -=-$dtectappl-=-"

if ( $?DTECT_USER ) then
	set transf="${transf} +-+ setenv DTECT_USER $DTECT_USER"
endif

if ( $?LM_LICENSE_FILE ) then
	set transf="${transf} +-+ setenv LM_LICENSE_FILE $LM_LICENSE_FILE"
endif

if ( $?DTECT_DEBUG ) then
    set transf="${transf} +-+ setenv DTECT_DEBUG $DTECT_DEBUG"
endif

if ( $?DISPLAY ) then
	set dnm=`echo $DISPLAY|sed 's/://'| awk '{print $1}'`
	if ( $dnm == local || $dnm == unix || $dnm == 0 || $dnm == "0.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		set nr=`echo $DISPLAY|sed 's/://'| awk '{print $2}'`
		if ( $nr == "" ) set nr=0
		setenv DISPLAY ${hona}":"${nr}.0
	else if ( $dnm == "1" || $dnm == "1.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		setenv DISPLAY ${hona}":1.0"
	endif
	set transf="${transf} +-+ setenv DISPLAY $DISPLAY"
endif

foreach var ( $transfvars )
    set dollarvar=`echo "$ ${var}" | sed "s/ //"`
    set varvalue=`eval "echo $dollarvar"`
    if ( $varvalue != "" ) then
	set transf="${transf} +-+ setenv $var $varvalue"
    endif
end

set transf="$transf +-+-+ $*"

set do_exec=`echo "${dtectappl}/bin/od_do_rmt" | sed -e "s% %\\ %g"`


if ( $?COMSPEC ) then 
    if ( ${rshcomm} == "rcmd" ) then

	set do_exec=`echo '+=+%DTECT_WINAPPL%\\bin\\od_dormt+=+' \
		     | sed -e "s% %\\ %g" -e 's%+=+%""%g'`

	set transffile="test.vars"

	echo "${transf}" > ${locprocdir}/${transffile}

	set transf=`echo "${remprocdir}/${transffile}" | sed -e 's% %#-#%g'`
	set procdir2=`echo "${remprocdir}" | sed -e 's% %#-#%g'`

	set transf="$transf $procdir2 $datahost $datadrive $datashare $USERNAME"

	set remotehost="\\${remotehost}"
	net use "${remotehost}" ${remotepass} \
		"/USER:${remotehost}\${user}" >& /dev/null
    endif
endif


if ( $?DTECT_DEBUG ) then
    echo ${rshcomm} ${remoteuser} "${remotehost}" ${do_exec} "$transf"
endif

${rshcomm} ${remoteuser} "${remotehost}" ${do_exec} $transf
exit 0
