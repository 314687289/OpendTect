#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) 1999,2002 de Groot-Bril Earth Sciences B.V.
# $Id: od_exec_rmt,v 1.6 2003-02-06 16:22:13 arend Exp $
#
# module remote startup script
#_______________________________________________________________________________

if ( ! $?dGB_APPL ) then
	echo "Error : dGB program not initialized."
	exit 1
endif

if ( $#argv < 1 ) then
	echo "Usage: $0 remote_host [--rexec remote_shell_command] ..."
endif

set rh=$1
shift

set rshcomm=rsh
if ( "$1" == "--rexec" ) then
	set rshcomm=$2
	shift ; shift
endif

set transf="setenv dGB_DATA $dGB_DATA +-+ setenv dGB_APPL $dGB_APPL"

if ( $?dGB_USER ) then
	set transf="${transf} +-+ setenv dGB_USER $dGB_USER"
endif

if ( $?LM_LICENSE_FILE ) then
	set transf="${transf} +-+ setenv LM_LICENSE_FILE $LM_LICENSE_FILE"
endif

if ( $?DISPLAY ) then
	set dnm=`echo $DISPLAY|sed 's/://'| awk '{print $1}'`
	if ( $dnm == local || $dnm == unix || $dnm == 0 || $dnm == "0.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		set nr=`echo $DISPLAY|sed 's/://'| awk '{print $2}'`
		if ( $nr == "" ) set nr=0
		setenv DISPLAY ${hona}":"${nr}.0
	else if ( $dnm == "1" || $dnm == "1.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		setenv DISPLAY ${hona}":1.0"
	endif
	set transf="${transf} +-+ setenv DISPLAY $DISPLAY"
endif

if ( $?dGB_TRANSFER_VARS ) then
    foreach var ( $dGB_TRANSFER_VARS )
	set dollarvar=`echo "$ ${var}" | sed "s/ //"`
	set varvalue=`eval "echo $dollarvar"`
	set transf="${transf} +-+ setenv $var $varvalue"
    end
endif

set transf="$transf +-+-+ $*"

${rshcomm} ${rh} ${dGB_APPL}/bin/dgb_do_rmt $transf
exit 0
