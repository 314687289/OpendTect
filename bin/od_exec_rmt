#!/bin/csh -f
#_______________________________________________________________________________
#
# (C) 1999,2002 de Groot-Bril Earth Sciences B.V.
# $Id: od_exec_rmt,v 1.8 2003-04-28 08:22:04 arend Exp $
#
# module remote startup script
#_______________________________________________________________________________

if ( $?dGB_DEBUG ) then
    echo "$*" > /tmp/dgbdebug.log
endif

if ( ! $?dGB_APPL ) then
	echo "Error : dGB program not initialized."
	exit 1
endif

if ( $#argv < 1 ) then
    echo "Usage: $0 remote_host [--rexec remote_shell_command]"
    echo "       [--transfervars <list of environment vars to transfer> -+-+-"
endif

set remotehost=$1
shift

set rshcomm=rsh
set transfvars=""
set remoteuser=""

next_arg:

if ( "$1" == "--rexec" ) then
	set rshcomm=$2
	shift ; shift
	goto next_arg
else if ( $1 == "--remoteuser" ) then
        set remoteuser=" -l $2"
        shift; shift
        goto next_arg
else if ( "$1" == "--transfervars" ) then
    next_var:
	shift
	if ( "$1" == "-+-+-" ) then
	    shift
	    goto next_arg
	endif

	set transfvars="$transfvars $1"
	goto next_var
endif

set transf="setenv dGB_DATA $dGB_DATA +-+ setenv dGB_APPL $dGB_APPL"

if ( $?dGB_USER ) then
	set transf="${transf} +-+ setenv dGB_USER $dGB_USER"
endif

if ( $?LM_LICENSE_FILE ) then
	set transf="${transf} +-+ setenv LM_LICENSE_FILE $LM_LICENSE_FILE"
endif

if ( $?DISPLAY ) then
	set dnm=`echo $DISPLAY|sed 's/://'| awk '{print $1}'`
	if ( $dnm == local || $dnm == unix || $dnm == 0 || $dnm == "0.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		set nr=`echo $DISPLAY|sed 's/://'| awk '{print $2}'`
		if ( $nr == "" ) set nr=0
		setenv DISPLAY ${hona}":"${nr}.0
	else if ( $dnm == "1" || $dnm == "1.0" ) then
		set hona=`uname -a|awk '{print $2}'`
		setenv DISPLAY ${hona}":1.0"
	endif
	set transf="${transf} +-+ setenv DISPLAY $DISPLAY"
endif

foreach var ( $transfvars )
    set dollarvar=`echo "$ ${var}" | sed "s/ //"`
    set varvalue=`eval "echo $dollarvar"`
    if ( $varvalue != "" ) then
	set transf="${transf} +-+ setenv $var $varvalue"
    endif
end

set transf="$transf +-+-+ $*"

if ( $?dGB_DEBUG ) then
    echo ${rshcomm} ${remotehost} ${dGB_APPL}/bin/dgb_do_rmt $transf >> /tmp/dgbdebug.log
endif

${rshcomm} ${remoteuser} ${remotehost} ${dGB_APPL}/bin/dgb_do_rmt $transf
exit 0
